XVEMSH ;DJB/VSHL**Help Text Menu [3/6/96 6:15pm];2017-08-15  5:01 PM
 ;;14.1;VICTORY PROG ENVIRONMENT;;Aug 16, 2017
 ; Original Code authored by David J. Bolduc 1985-2005
 ;
EN ;Entry Point
 NEW CNT,COL,COLCNT,COLUMNS,HD,LAST,PROMPT,SET,SPACES,WIDTH,WRITE
 NEW CNTOLD,DX,DY,FLAGQ,I,OPT,TEST,TXT,XVVS,X,Y
 I $G(XVVSHL)'="RUN" NEW XVV
 S FLAGQ=0 D INIT,INIT^XVEMSHY Q:FLAGQ
 X XVVS("RM0")
TOP ;
 F  S FLAGQ=0 D HD^XVEMSHY,LIST,GETOPT Q:FLAGQ  D RUN Q:FLAGQ
EX ;
 X XVVS("RM0") W @XVV("IOF")
 Q
GETOPT ;
 X PROMPT S OPT=$$READ^XVEMKRN()
 I OPT="^" S FLAGQ=1 Q
 I ",<ESC>,<F1E>,<F1Q>,<TAB>,<TO>,"[(","_XVV("K")_",") S FLAGQ=1 Q
 I XVV("K")="<RET>" S OPT=CNT Q
 I XVV("K")?1"<A"1A1">" S CNTOLD=CNT D ARROW S OPT=CNT D REDRAW G GETOPT
 S OPT=$$ALLCAPS^XVEMKU(OPT),TEST=0 D  I TEST Q
 . F I=1:1 S X=$P($T(MENU+I),";",5) Q:X=""  I $E(X,1,$L(OPT))=OPT S (CNT,OPT)=I,COL=$P($T(MENU+I),";",3),TEST=1 Q
 G GETOPT
ARROW ;Arrow Keys
 I "<AU>,<AD>"[XVV("K") D  S COL=$P($T(MENU+CNT),";",3) Q
 . I XVV("K")="<AU>" S CNT=CNT-1 S:CNT<1 CNT=LAST Q
 . I XVV("K")="<AD>" S CNT=CNT+1 S:CNT>LAST CNT=1
 I XVV("K")="<AR>" Q:COL=COLCNT  D  D ADJUST Q
 . S CNT=CNT+COL(COL),COL=COL+1 S:CNT>LAST CNT=LAST
 I XVV("K")="<AL>" Q:COL=1  D  D ADJUST Q
 . S COL=COL-1,CNT=CNT-COL(COL)
 Q
RUN ;Run selected routine
 S X=$P($T(MENU+OPT),";",6) I X="QUIT" S FLAGQ=1 Q
 NEW CNT,COL,COLCNT,COLUMNS,HD,LAST,PROMPT,SET,SPACES,WIDTH,WRITE
 I X]"" W @XVV("IOF") D @X X XVVS("RM0")
 Q
LIST ;List Menu Options
 F I=1:1 S TXT=$T(MENU+I) Q:TXT=""!(TXT[";***")   X SET,WRITE
 S TXT=$T(MENU+CNT) Q:TXT=""  X SET W @XVV("RON") X WRITE W @XVV("ROFF")
 F I=1:1 S TXT=$T(TOPICS+I) Q:TXT=""!(TXT[";***")   X SET,WRITE
 Q
REDRAW ;User moved cursor
 S TXT=$T(MENU+CNTOLD) X SET,WRITE
 S TXT=$T(MENU+CNT) X SET W @XVV("RON") X WRITE W @XVV("ROFF")
 Q
ADJUST ;Adjust CNT when you switch columns.
 F  Q:$P($T(MENU+CNT),";",3)=COL  S CNT=CNT-1
 Q
INIT ;Initialize variables
 S COLUMNS="7^7",WIDTH=21
 S HD="V S H E L L   H E L P   M E N U"
 Q
MENU ;MENU OPTIONS
 ;;1;Introduction;INTRODUCTION;HELP^XVEMKT("INTRO");7;4
 ;;1;Protection;PROTECTION;HELP^XVEMKT("PROT");7;6
 ;;1;Keyboard;KEYBOARD;HELP^XVEMKT("KEYS");7;8
 ;;1;VA KERNEL;VA KERNEL;HELP^XVEMKT("KERN");7;10
 ;;1;QWIK Commands;QWIK COMMANDS;HELP^XVEMKT("QWIK");7;12
 ;;1;System QWIKs;SYSTEM QWIKS;HELP^XVEMKT("SYSTEM");7;14
 ;;1;User QWIKs;USER QWIKS;HELP^XVEMKT("USER");7;16
 ;;2;Boxes;BOXES;HELP^XVEMKT("BOXES");41;4
 ;;2;Vendor Configurations;VENDOR CONFIGURATIONS;HELP^XVEMKT("VENDOR");41;6
 ;;2;Command Line History;COMMAND LINE HISTORY;HELP^XVEMKT("CLH");41;8
 ;;2;Programmer Tools;PROGRAMMER TOOLS;HELP^XVEMKT("PGM");41;10
 ;;2;Parameter Passing;PARAMETER PASSING;HELP^XVEMKT("PARAM");41;12
 ;;2;Miscellaneous;MISCELLANEOUS;HELP^XVEMKT("MISC");41;14
 ;;2;Quit;QUIT;QUIT;41;16
 ;;***
TOPICS ;Topic subheadings
 ;;***
