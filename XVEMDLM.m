XVEMDLM ;DJB/VEDD**Menu,Find,Goto [11/25/95 12:22pm];2017-08-15  12:17 PM
 ;;14.1;VICTORY PROG ENVIRONMENT;;Aug 16, 2017
 ; Original Code authored by David J. Bolduc 1985-2005
 ;
RUN(Z1) ;
 I Z1?1.N D INDFLD Q
 I Z1="DA" D ^XVEMDLD Q
 I Z1="F" D FIND Q
 I Z1="G" D GOTO Q
 I Z1="P" D ^XVEMDLB Q
 D ENDSCR^XVEMKT2
 I Z1="<ESCH>" D HELP^XVEMKT2 Q
 I Z1="?" D HELP^XVEMKT("VEDD2") Q
 I Z1="I" D ^XVEMDI S FLAGQ=0 Q
 I Z1="N" D ^XVEMDN Q
 I Z1="VGL" D VGL2^XVEMDY Q
 Q
INDFLD ;Print Indiv Fld DD
 NEW FILE,FLAGQ,FNUM
 I '$D(^TMP("XVV","VEDD"_VEDDS,$J,Z1)) D MSG^XVEMDUM(4,1) Q
 D ENDSCR^XVEMKT2
 S FILE=$P(^TMP("XVV","VEDD"_VEDDS,$J,Z1),U)
 S FNUM=$P(^(Z1),U,2)
 S FLAGQ=0 D INDIV^XVEMKI1(FILE,FNUM) Q:FLAGQ  D PAUSE^XVEMKC(1)
 Q
FIND ;Find a field. Return FLAGFIND if search is valid
 R "   Enter FIELD NAME: ",FLAGFIND:XVV("TIME")
 S:'$T FLAGFIND="^" I "^"[FLAGFIND KILL FLAGFIND Q
 I "??"[FLAGFIND D MSG^XVEMDUM(5) G FIND
 I $L(FLAGFIND)>30!(FLAGFIND["""")!(FLAGFIND["=") D MSG^XVEMDUM(6) G FIND
 Q
GOTO ;Goto line number
 NEW ND,PKG,X S PKG="ID"_VEDDS
 S ND=$$GETREF^XVEMKTR(PKG) Q:ND="^"
 I ND="***" W $C(7) Q
 I '$D(^TMP("XVV","VEDD"_VEDDS,$J,ND)) D  Q:ND'>0
 . S ND=$O(^TMP("XVV","VEDD"_VEDDS,$J,ND)) Q:ND>0
 . S ND=$O(^TMP("XVV","VEDD"_VEDDS,$J,ND),-1)
 S XVVT("TOP")=$$GETSCR^XVEMKTR(ND,PKG)
 Q
