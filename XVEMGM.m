XVEMGM ;DJB/VGL**Main Menu ;2017-09-18  5:10 PM
 ;;14.1;VICTORY PROG ENVIRONMENT;;Aug 16, 2017
 ; Original Code authored by David J. Bolduc 1985-2005
 ; New Error trap in RUN+1 (c) 2016 Sam Habiel
 ;
READ ;Get input
 ;Next: Quit if SKIP NODE
 I $G(FLAGSKIP),$G(SKIPHLD)]"" D SKIPCHK^XVEMGO Q
 ;Next: Quit if CODE SEARCH
 I $P(XVVT("STATUS"),"^",4)="SEARCH" S XVVT("TOP")=XVVT("TOP")+1 Q
 NEW KEY,PKG
READ1 S PKG="IG"_GLS W @XVVS("CON") ;Turn cursor back on
 S KEY=$$READ^XVEMKTM(PKG) Q:KEY="QUIT"
 I KEY="<TAB>" D  ;
 . S TABHLD=XVVT("HLN")_"^"_XVVT("H$Y") ;Keeps highlight at same node
 . S KEY=XVVT("HLN")-1,KEY=$G(^TMP("XVV",PKG,$J,"SCR",KEY))
 . S:KEY']"" KEY="***"
 I KEY?1"S"1.N G:$$ENDFILE^XVEMKTM() READ1 D  D REDRAW^XVEMKT2() Q
 . S FLAGSKIP=+$E(KEY,2,99) D SKIP^XVEMGO
 I ",?,<ESCH>,A,C,ER,ES,EV,G,M,SA,SC,UN,VEDD,"'[(","_KEY_","),KEY'?1.N W $C(7) G READ1
 D RUN(KEY) Q:FLAGE  H:'$D(%ut) 1 D REDRAW^XVEMKT2()
 D:CODE'=0 CDSRCH^XVEMGO
 Q
RUN(Z1) ;Z1=Opt selected
 N $ESTACK,$ETRAP S $ETRAP="D ERROR^XVEMGM,UNWIND^XVEMSY"
 NEW FLAGQ S FLAGQ=0
 I Z1?1.N D PIECE^XVEMGM1 Q
 I ",A,C,ES,EV,SC,"[(","_Z1_",") I '$$CHECK^XVEMGM1() Q
 I Z1="ER" D EDITR^XVEMGE Q
 I Z1="ES" D EDITS^XVEMGE Q
 I Z1="EV" D EDITV^XVEMGE Q
 I Z1="G" D GOTO^XVEMGMC Q
 I Z1="SA" D SAVE^XVEMGS Q
 I Z1="SC" D STRIP^XVEMGE Q
 I Z1="UN" D UNSAVE^XVEMGS Q
 I Z1="<ESCH>" D HELP^XVEMKT2 Q
 D ENDSCR^XVEMKT2
 I Z1["?" D HELP^XVEMKT("VGL2") Q
 ;I Z1="A" NEW FLAGPRM D START^XVEMG Q
 I Z1="A" D ALT^XVEMGM1 Q
 I Z1="C" D CODE^XVEMGO Q
 I Z1="M" D MORE^XVEMGM1 Q
 I Z1="VEDD" D VEDD^XVEMGY Q
 Q
ERROR ;
 D ENDSCR^XVEMKT2,ERRMSG^XVEMKU1("VGL/RUN"),PAUSE^XVEMKU(1)
 Q
