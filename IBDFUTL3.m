IBDFUTL3	;ALB/MAF - MAINTENANCE UTILITY CONT. ;4/24/95
	;;3.0;AUTOMATED INFO COLLECTION SYS;**63**;APR 24, 1997;Build 80
	;
	;
	;
REPLACE	;  -- Replace invalid code with another valid code... it will be in
	;     the same place as the old invalid code.
	N IBDFVALM,VALMY,IBBLK,IBDFSLC,IBDFSLC1,IBDFSLC2,IBFORM,IBGRP,IBLIST,DA,IBSEL,ORDER,IEN,IBDN,IBDX,IBD9,IBD10,IBDQUIT
	N IBDN,IBDX,IBD9,IBD10
	S IBDQUIT=0
	S VALMBCK=""
	D EN^VALM2($G(XQORNOD(0)),"S") G REP:'$O(VALMY(0)) S IBDFVALM=0
	D FULL^VALM1 S VALMBCK="R"
	F IBDFVALM=0:0 S IBDFVALM=$O(VALMY(IBDFVALM)) Q:IBDFVALM']""  S (IBDFSEL,DA)=$P($G(^TMP("CPTIDX",$J,IBDFVALM)),"^",4) I DA]"" S IBDFSLC=$G(^IBE(357.3,DA,0)),IBDFSLC1=$G(^IBE(357.3,DA,1,1,0)),IBDFSLC2=$G(^IBE(357.3,DA,1,2,0)) D
	.S IBFORM=$P($G(^TMP("CPTIDX",$J,IBDFVALM)),"^",5)
	.S IBGRP=$P(IBDFSLC,"^",4)
	.S IBLIST=$P(IBDFSLC,"^",3)
	.S ORDER=$P(IBDFSLC,"^",5)
	.S IBBLK=$P($G(^TMP("CPTIDX",$J,IBDFVALM)),"^",6)
	D REPLC(IBLIST,IBGRP,ORDER,.IBSEL,IBBLK,IBFORM,.IBDQUIT)
	Q:IBDQUIT
	S IBDN="",(IBD9,IBD10)=0 F  S IBDN=$O(^IBE(357.2,"C",IBBLK,IBDN)) Q:IBDN=""  S IBDX=$P($G(^IBE(357.2,IBDN,0)),U,11) I IBDX?1.N S IBDX=$E($P($G(^IBE(357.6,IBDX,0)),U,1),1,30) D
	.I IBDX="DG SELECT ICD-9 DIAGNOSIS CODE" S IBD9=1
	.I IBDX="DG SELECT ICD-10 DIAGNOSIS COD" S IBD10=1
	;Now update history if ICD-9 or ICD-10 was present before or after the change
	N IBDX
	I IBD9 S IBDX=$$CSUPD357^IBDUTICD(IBFORM,1,"",$$NOW^XLFDT(),DUZ)
	I IBD10 S IBDX=$$CSUPD357^IBDUTICD(IBFORM,30,"",$$NOW^XLFDT(),DUZ)
	K IBDF,^TMP("UTIL",$J) D INIT^IBDFUTL S VALMBCK="R" Q
	;
	;
REPLC(IBLIST,IBGRP,ORDER,IBSEL,IBBLK,IBFORM,IBDQUIT)	;allows the user to add a selection to the selection group for replacement - returns 0 if it was done, 1 otherwise
	N SUB,IBRTN,IBDSERCH
	;
	Q:'$$FORMDSCR^IBDFU1C(.IBFORM)
	Q:$$BLKDESCR^IBDFU1B(.IBBLK) 1
	Q:$$LSTDESCR^IBDFU1(.IBLIST) 1
	S IBRTN=IBLIST("RTN")
	D RTNDSCR^IBDFU1B(.IBRTN)
	;N QUIT S QUIT=0
	I IBRTN("ACTION")'=3 D NOGOOD^IBDF4 Q 1
	S IBDSERCH=2  ;Lexicon search.
	K @IBRTN("DATA_LOCATION")
	I '$$DORTN^IBDFU1B(.IBRTN,IBDSERCH) D NOGOOD^IBDF4 Q 1
	I $D(DUOUT)!($D(DTOUT))!('$D(@IBRTN("DATA_LOCATION"))) D:$D(IBSEL) KILL3573(IBSEL) S IBDQUIT=1 Q
	D ADDREC^IBDF4(.IBDQUIT,ORDER,.IBSEL,$P($G(@IBRTN("DATA_LOCATION")),U)) ;edits and adds the selection
	I IBDQUIT=1 D KILL3573(IBSEL)
	K @IBRTN("DATA_LOCATION")
	;  -- If a selection has been chosen,  the old node is killed off and
	;     the block/selection list is updated.
	I IBDQUIT Q
	S DA=IBDFSEL,DIK="^IBE(357.3," D ^DIK K DIK D BLKCHNG^IBDF19(IBFORM,IBBLK)
	Q
	;To kill incomplete entries in ^IBE(357.3
KILL3573(IBDSEL)	;
	N DA,DIK
	S DA=IBDSEL,DIK="^IBE(357.3," D ^DIK K DIK ;D BLKCHNG^IBDF19(IBFORM,IBBLK)
	Q
REP	K IBDF D INIT^IBDFUTL S VALMBG=1,VALMBCK="R"
	Q
