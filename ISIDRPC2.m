ISIDRPC2 ;ISI/BT - ISI DICOM Gateway RPCs ; 04/27/2016 12:27 PM
 ;;1.0;DICOM CORRECT ALERT;**local**;27-April-2016;Build 1
 QUIT
 ;
 ; ##### Get DICOM INCORRECT STUDIES
 ; 
 ; INPUT
 ;   LOCATION   DICOM LOCATION
 ;   MACHID     DICOM HOSTNAME
 ;
 ; OUTPUT
 ;   OUT        Array of studies need to be corrected
 ;   OUT(1) :   Number of Studies or ERRORCODE,Error message
 ;   OUT(2..n)  FAILED STUDY Information - IEN^FAILED REASON^PATIENT ID^CASE NUMBER^DATE ENTERED^ALERTED
 ;
INCSTDY(OUT,LOCATION,MACHID) ; RPC [ISI DICOM INCORRECT STUDIES]
 ; Check for images needing corrections
 K OUT
 I '$G(LOCATION) S OUT(1)="-1,No Location Specified" QUIT
 I $G(MACHID)="" S OUT(1)="-2,No Gateway Specified" QUIT
 ;
 S OUT(1)=0
 QUIT:'$O(^MAGD(2006.575,0))
 QUIT:'$D(^MAGD(2006.575,"F",LOCATION))
 ;
 N IEN,MAGD,FAILRSN,PID,CASENO,ENTRDT
 N MAGMACH,ALERTED
 N CNT S CNT=1
 N STUDY S STUDY=""
 ;
 F  S STUDY=$O(^MAGD(2006.575,"F",LOCATION,STUDY)) QUIT:STUDY=""  D
 . S IEN=$O(^MAGD(2006.575,"F",LOCATION,STUDY,0)) QUIT:'IEN
 . K MAGD S IEN=IEN_","
 . D GETS^DIQ(2006.575,IEN,"1;2;5;7;28",,"MAGD") QUIT:'$D(MAGD)
 . S MAGMACH=$G(MAGD(2006.575,IEN,28))
 . S ALERTED=$P($G(^MAGD(2006.575,+IEN,"ALERTED")),U)="A"
 . ;S ALERTED=$G(MAGD(2006.575,IEN,38))="ALERTED"
 . I MACHID=MAGMACH D
 . . S FAILRSN=$G(MAGD(2006.575,IEN,1))
 . . S PID=$G(MAGD(2006.575,IEN,2))
 . . S CASENO=$G(MAGD(2006.575,IEN,5))
 . . S ENTRDT=$G(MAGD(2006.575,IEN,7))
 . . S CNT=CNT+1,OUT(CNT)=+IEN_U_FAILRSN_U_PID_U_CASENO_U_ENTRDT_U_ALERTED
 ;
 S OUT(1)=CNT-1
 QUIT
 ;
 ;
 ; ##### Flag studies to "Alerted"
 ; 
 ; INPUT
 ;   STUDIES    Array of Study UID
 ;
 ; OUTPUT
 ;   OUT        Number of Studies that are flagged to "Alerted"
 ;
ALERTED(OUT,STUDIES) ; RPC [ISI DICOM STUDIES ALERTED]
 ;
 N CNT S CNT=0
 F  S CNT=$O(STUDIES(CNT)) Q:'CNT  D
 . S $P(^MAGD(2006.575,STUDIES(CNT),"ALERTED"),U)="A"
 . ;K ERR,FDA S FDA(2006.575,STUDIES(CNT)_",",38)="A"
 . ;D FILE^DIE("","FDA","ERR")
 S OUT=CNT
 QUIT
