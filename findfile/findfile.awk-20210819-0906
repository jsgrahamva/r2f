#
# Associate files with routines
#

# 20210816 - Work on this:  i
# a) Allow for multiple filenums per line
#    1) DIE line
#    2) Other lines
# b) S DIE="^IB(",DA=IBN,DR=".05////"_$S(IBERR="":3,1:9)
#    D ^DIE K DIE,DR,DA

#
# Find file number value for parameter variable
#
BEGIN {
	debug                = 0;
	tags["FILE"]         = 2;
	tags["UPDATE"]       = 2;
	tags["HELP"]         = 1;
	tags["WP"]           = 1;
	tags["CHK"]          = 1;
	linesfound[" "][" "] = "";
}

function getFilenumValue(routine,linecnt2,var, linecnt3, fnd, i) {
	linecnt3 = linecnt2 - 1; 
	len      = length(var) + 2;
	line     = lines[routine][linecnt3]; 
	#
	# Stop when tag line is found
	#
	while (line ~ /^( |\t)/) {
		if (debug) print "   Looking for Set <var> = " line
		fnd = index(line,"S " var); # Find Set <var>
		if (fnd > 0) {i
			if (debug) print "      fnd > 0"
			#
			# Make sure it's a set 
			#
			if (substr(line,fnd-1) ~ /[ |\t]/) {
				if (debug) print "      It's a set"
				#
				# Make sure the rest of the line is:
				# . 0 or more spaces
				# . =
				# . 0 or more spaces
				#
				if (substr(line,fnd+len) ~ / *= */) {
					if (debug) print "      found the ="
					line = substr(line,fnd+len); 
					fnd  = index(line,"="); 
					line = substr(line,fnd+1);
					pos  = 0; 
					#
					# Looking for 1st non-space character after =
					#
					for (i = 1; i<= length(line); i++) { 
						if (substr(line,i,1) != " ") { 
							pos = i;
							break
						} 
					}
					if (pos > 0) {
						if (debug) print "      pos > 0"
						split(substr(line,pos),arr," "); 
						filenum = arr[1]; 
						if (debug) print "      filenum = |" filenum "|"
						#
						# If parameter value is not a $Select statement
						#
						if (filenum !~ /\$S/) {
							if (debug) print "      Not a $Select = " filenum
							if (filenum = "\"\"") {
								if (debug) print "      substr(line,pos) = '" substr(line,pos) "'"
								if (substr(line,pos + 2) ~ / [F|f] /) {
									filenum = "<LOOP>"
									if (debug) print "      1:filenum = " filenum
								}
								else {
									filenum = "<Unknown>"
									if (debug) print "      2:filenum = " filenum
								}
							}
							if (debug) print       "      A: filenum = " filenum
							saveFilenum(routine,filenum)
						}
						else {
							#
							# Get values for $Select statement
							#
							if (debug) print "      $Select"
							filenum = substr(filenum,4,length(filenum)-4);
							n       = split(filenum,arr,",");
							for (i = 1; i <= n; i++) {
								phrase                  = arr[i];
								split(phrase,arr2,":");
								filenum                 = arr2[2];
								gsub(/"/,"",filenum);
								if (filenum != "") {
									if (debug) print "      B: filenum = '" filenum "'"
									saveFilenum(routine,filenum,lines[routine][linecnt3])
								}
							}
						}
					} 
				} 
			}
		}
		line = lines[routine][--linecnt3]
	}

}

#
# Check for various <tag>^DIE(
#
function getPieceNum(line,fnd) {
	piecenum = 0;
	for (tag in tags) {
		len = length(tag);
		if (substr(line,fnd - len - 4, len) == tag) {
			piecenum = tags[tag];
			break
		}
	}

	return piecenum

}

function printRoutinesAndFiles(routine) {
	if ((routine in linesfound) == 1) {
		for (linecnt in linesfound[routine]) { 
			line = lines[routine][linecnt];
			#
			# Ignore comment lines
			#
			if (substr(line,2,1) == ";") {
				continue
			}
			while (1) {
				fnd  = index(line,"^DIE(");
				#
				# Looking for ^DIE(
				#
	
				if (fnd == 0) {
					break
				}
				fnd      = fnd + 4;
				piecenum = getPieceNum(line,fnd);
				if (debug) print "line     = " line
				if (debug) print "piecenum = " piecenum
	
				#
				# Did not find the tags we were looking for
				#
				if (piecenum == 0) {
					line = substr(line,fnd);
					continue
				}
				#
				# Looking for parameter which holds the name of the file
				#
				if (debug) print "line 1 = " line
				line = substr(line,fnd+1); 
				if (debug) print "line 2 = " line
				n    = split(line,arr,",");
				split(arr[piecenum],arr2,")"); 
				filenum = arr2[1]; 
				gsub(/"/,"",filenum); 
				if (debug) print "filenum = " filenum
				if (filenum ~ /^[0-9]+$/) {
					if (debug) print "A"
					saveFilenum(routine,filenum,lines[routine][linecnt])
				}
				else {
					if (debug) print "B1"
					if (filenum ~ /^[0-9]+\.[0-9]+$/) {
						if (debug) print "B2"
						saveFilenum(routine,filenum,lines[routine][linecnt])
					}

					else { 
						if (debug) print "B3"
						len      = length(filenum); 
						line2    = lines[routine][linecnt]; 
						scanLines(line2,filenum,len,routine,linecnt)
					}
				}
			} 
		}
	}
}

#
# Save filenum/routine
#
function saveFilenum(routine,filenum,line) {
	if (index(filenum,",") > 0) {
		filenum = "Unknown: " routine " / " line
	}
	else {
		if (filenum ~ /"[0-9]+\.[0-9]+"/) {
			gsub(/"/,"",filenum)
		}
	}
	rtnfile[routine][filenum] = ""; 
	filenums[filenum]         = "" 
}

#
# Scanning lines above <tag>^DIE( for file number
#
function scanLines(line,var,len,routine,linecnt2) {
	#
	# Stop at the line with the tag
	#
	while (line ~ /^( |\t)/) {
		if (debug) print "scanLines: line = " line
		flg = 0;
		#
		# Ignore comment lines
		#
		if (substr(line,2,1) == ";") {
			line = lines[routine][--linecnt2]
			continue
		}
		fnd = index(line,"FDA^DILF(");
		if (fnd > 0) {
			if (debug) print "scan A"
			line    = substr(line,fnd + 9);
			split(line,arr,",");
			split(arr[1],arr2,")");
			filenum = arr2[1];
			flg     = 1
		}
		else { 
			if (debug) print "scan B"
			#
			# Looking for line with parameter/name in it
			#
			fnd = index(line,var "("); 
			if (fnd > 0) { 
				line                 = substr(line,fnd + len + 1); 
				split(line,arr,","); 
				filenum              = arr[1]; 
				if (filenum != "") { 
					flg                  = 1 
				}
			}
		}
		if (debug) print "scan C"
		if (flg == 1) {
			#
			# If parameter name is alphabetic, search for corresponding value
			#
			if (debug) print "scan D"
			if (filenum ~ /[A-Z]+/) {
				if (debug) print "scan E: " filenum
				getFilenumValue(routine,linecnt2,filenum)
			}
			else { 
				#
				# Otherwise, store value
				#
				if (debug) print "scan F: " filenum
				saveFilenum(routine,filenum)
			}
		}
		line = lines[routine][--linecnt2]
	}
}

(FNR == 1) {
	if (NR != 1) {
		printRoutinesAndFiles(routine)
	}
	routine = FILENAME
}

{ 
	lines[routine][FNR] = $0; 
	if (index($0,"^DIE(")) { linesfound[routine][FNR] = 1 } 
} 

END {
	printRoutinesAndFiles(routine);
	n1 = asorti(rtnfile,rtnfile2);
	n2 = asorti(filenums,filenums2,"@ind_num_asc");
	if (debug) print "# of filenums = " n2
	header = "Routines";
	for (i = 1; i <= n2; i++) {
		filenum            = filenums2[i];
		header             = header "," filenum;
		filenums3[filenum] = i
	}
	print header;
	#
	# For each routine
	#
	for (i = 1; i <= n1; i++) {
		rtn  = rtnfile2[i];
		data = rtn;
		#
		# For each file number
		for (j = 1; j <= n2; j++) {
			filenum    = filenums2[j];
			filenumpos = filenums3[filenum];
			#
			# Routine accesses the file number
			#
			if (filenum in rtnfile[rtn]) { 
				data = data ",X"
			}
			#
			# Routine does not access the file number
			#
			else {
				data = data ","
			}
		}
		print data
	}
}
